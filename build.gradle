plugins {
  id 'java'
  id 'maven-publish'
  id 'signing'
}

group = 'org.legendofdragoon'
version = '1.0'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

java {
  sourceCompatibility = targetCompatibility = 1.21
}

compileJava.options.encoding = 'UTF-8'

repositories {
  mavenCentral()
}

dependencies {
  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.14.1'
  implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.17.1'
  implementation group: 'org.fusesource.jansi', name: 'jansi', version: '2.3.2'

  implementation group: 'org.reflections', name: 'reflections', version: '0.10.2'
  // To quiet no logger found for reflections
  implementation group: 'org.slf4j', name: 'slf4j-nop', version: '2.0.7'

  implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
}

tasks.register('sourceJar', Jar) {
  from sourceSets.main.allJava
}

tasks.withType(Sign).configureEach {
  onlyIf { isReleaseVersion }
}

java {
  withJavadocJar()
  withSourcesJar()
}

artifacts {
  archives javadocJar, sourcesJar
}

publishing {
  repositories {
    maven {
      final def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
      final def snapshotRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
      url = isReleaseVersion ? releaseRepo : snapshotRepo
      credentials {
        username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
        password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
      }
    }
  }

  publications {
    mavenJava(MavenPublication) {
      artifactId = 'mod-loader'
      from components.java
      pom {
        name = 'Severed Chains Mod Loader'
        description = 'The mod loader used by various Severed Chains projects'
        url = 'https://github.com/Legend-of-Dragoon-Modding/Mod-Loader'
        packaging = 'jar'
        licenses {
          license {
            name = 'GNU Affero General Public License, Version 3.0'
            url = 'https://www.gnu.org/licenses/agpl-3.0.en.html'
          }
        }
        developers {
          developer {
            id = 'Monoxide'
            name = 'Corey Frenette'
            email = 'lordmonoxide@gmail.com'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/Legend-of-Dragoon-Modding/Mod-Loader.git'
          developerConnection = 'scm:git:ssh://github.com/Legend-of-Dragoon-Modding/Mod-Loader.git'
          url = 'https://github.com/Legend-of-Dragoon-Modding/Mod-Loader/'
        }
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}

javadoc {
  if(JavaVersion.current().isJava9Compatible()) {
    options.addBooleanOption('html5', true)
  }
}
